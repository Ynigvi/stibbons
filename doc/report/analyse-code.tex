\section{Outils d'analyse}

\subsection{Flex}

Flex est une version libre de l'analyseur lexical Lex. Il est généralement associé à l'analyseur syntaxique GNU Bison, la version GNU de Yacc.
Il lit les fichiers d'entrée donnés pour obtenir la description de l'analyseur à générer. La description est une liste de paires d'expressions rationnelles et de code C, appelées règles. 

Un fichier flex est composé de plusieurs parties. La première contient une partie optionnelle de définition, encadrée par les symboles \verb|%{ %}| (cf. listing \ref{flex-definition}), ainsi que des options pour flex (cf. listing~\ref{flex-options}). La seconde partie est une partie obligatoire de règles, commençant par \verb|%%| (cf. listing~\ref{flex-regles}), tandis que la dernière partie est une nouvelle partie optionnelle, débutée par \verb|%%|, pouvant contenir des fonctions C/C++ définies par l'utilisateur (cf. listing~\ref{flex-fonctions}).

\begin{lstlisting}[caption=Partie définition d'un fichier flex,label=flex-definition]
%{
 int yyFlexLexer::yywrap() {
	return 1;
 }
%}
\end{lstlisting}

\begin{lstlisting}[caption=Options flex,label=flex-options]
%option c++ 
%option nodefault 
\end{lstlisting}

\begin{lstlisting}[label=flex-regles,caption=Partie règles de flex]
%%
#([a-f0-9]{6}|[a-f0-9]{3}) {
							pyylval->v=make_shared<stibbons::Color>(yytext); 
							return yy::parser::token::COLOR;
						   }
\end{lstlisting}
\begin{lstlisting}[label=flex-fonctions,caption=Partie fonctions de flex]
%%
int main(){
	// ...
}
\end{lstlisting}

La transformation en code C++ se fait par compilation via l'appel à l'application \verb|flex -+ exemple.l+|. La fonction d'analyse ainsi générée se nomme \verb|yylex()|.
Il faut par la suite penser à compiler le programme en liant la bibliothèque flex via le flag \verb|-lfl|.

\subsection{Bison}

GNU Bison est une version de yacc, un outil d'analyse syntaxique (cf.~\ref{analyse-syntaxique}). Il génère un analyseur syntaxique ascendant utilisant un automate à pile (dérivation à droite, on remplace le symbole non terminal le plus à droite).

Son fonctionnement est le suivant~: à chaque règle de grammaire, on associe des actions (instructions d'un langage). L'analyseur généré essaie de reconnaître un mot du langage défini par la grammaire. Il exécute les actions pour chaque règle reconnue.

Comme pour Flex, un fichier Bison est composé de trois parties. La première partie, facultative, contient une liste de définition C/C++, d'options bisons ainsi que de définition de jetons (cf. listing~\ref{bison-definition}), la seconde partie, contenue entre \verb|%%|, contient les règles (cf. listing~\ref{bison-regle}) et une dernière optionnelle de C/C++.

\begin{lstlisting}[label=bison-definition,caption=Definition C++ en bison]
%skeleton "lalr1.cc"
%defines
%locations
%parse-param { stibbons::FlexScanner &scanner }
%parse-param { stibbons::TreePtr t }
%parse-param { stibbons::TablePtr w }
%lex-param   { stibbons::FlexScanner &scanner }

%code requires {
	namespace stibbons {
		class FlexScanner;
	}

	std::string toString(const int& tok);
}

%token IF   "if"
%token ELSE "else" 
%token FCT  "function"
\end{lstlisting}

\begin{lstlisting}[label=bison-regle,caption=Règles de grammaire en bison]
%%

//Storage of conditionnal expression
selection : IF expr statement 
{
  stibbons::TreePtr t1 = make_shared<stibbons::Tree>(yy::parser::token::IF,nullptr);
  t1->addChild($2);
  t1->addChild($3);
  t1->setPosition({@1.begin.line,@1.begin.column});
  $$ = t1;
}
| IF expr statement ELSE statement
{
  stibbons::TreePtr t1 = make_shared<stibbons::Tree>(yy::parser::token::IF,nullptr);
  t1->addChild($2);
  t1->addChild($3);
  t1->addChild($5);
  t1->setPosition({@1.begin.line,@1.begin.column});
  $$ = t1;
};


%%
\end{lstlisting}

Les différents types de jeton sont déclarés via l'instruction \verb|%token NOM_DU_JETON| dans la première partie. On peut également définir le type C/C++ de la valeur du jeton via l'instruction \verb|%union|, ou en redéfinissant la macro \verb|YYSTYPE| (cf.~\ref{exemple-union} et \ref{exemple-yystype}).

\begin{lstlisting}[label=exemple-union,caption=Exemple du type des valeurs des jetons avant la 0.3]
%union{
  stibbons::Value* v;
  stibbons::Tree* tr;
}
\end{lstlisting}

\begin{lstlisting}[label=exemple-yystype,caption=Exemple du type des valeurs des jetons en 1.0]
#define YYSTYPE struct { stibbons::ValuePtr v; stibbons::TreePtr tr; int tok; }
\end{lstlisting}

Si on veut un analyseur syntaxique en C++, il faut utiliser un squelette de parseur C++ en utilisant soit l'option bison \verb|-skeleton=lalr1.cc|, soit en utilisant la directive \verb|%skeleton "lalr1.cc"|. 

La transformation en code C++ se fait par compilation via l'appel à l'application \verb|bison -ydt exemple.y+|. La fonction d'analyse ainsi générée se nomme \verb|yyparse()| et fait appel en interne à \verb|yylex()|.
