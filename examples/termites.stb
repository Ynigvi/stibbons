%world_width 100
%world_height 100
%zone_width 5
%zone_height 5
%x_warp true
%y_warp true

agent termite () {
	function go () {
		search_for_chip ()
		find_new_pile ()
		put_down_chip ()
	}

	function search_for_chip () {
		continue = true
		while continue {
			if (zone.color == yellow) {
				zone.color = black
				color = red
				fd 20
				continue = false
			}
			else {
				wiggle()
			}
		}
	}

	function find_new_pile () {
		while (zone.color != yellow) {
			wiggle()
		}
	}

	function put_down_chip () {
		continue = true
		while continue {
			if (zone.color == black) {
				zone.color = yellow
				color = white
				get_away()
				continue = false
			}
			else {
				rt rand()
				fd 1
			}
		}
	}

	function get_away () {
		rt rand()
		fd 20
		while (zone.color != black) {
			rt rand()
			fd 20
		}
	}

	function wiggle () {
		fd 1
		rt rand() % 50
		lt rand() % 50
	}

	teleport(rand() % world.max_x, rand() % world.max_y, rand())

	while (true) {
		go()
	}
}

function init_zone() {
	rand = world.rand
	test = rand() % 5
	if (test == 0) {
		color = world.yellow
	}
	else {
		color = world.black
	}
}

ask_zones (init_zone)

20 new termite()
