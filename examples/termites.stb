%world-width 250
%world-height 250
%zone-width 2
%zone-height 2
%x-warp true
%y-warp true

agent termite () {
	function go () {
		search-for-chip ()
		find-new-pile ()
		put-down-chip ()
	}

	function search-for-chip () {
		continue = true
		while continue {
			if (zone.color == yellow) {
				zone.color = black
				color = red
				fd 20
				continue = false
			}
			else {
				wiggle()
			}
		}
	}

	function find-new-pile () {
		while (zone.color != yellow) {
			wiggle()
		}
	}

	function put-down-chip () {
		continue = true
		while continue {
			if (zone.color == black) {
				zone.color = yellow
				color = white
				get-away()
				continue = false
			}
			else {
				rt rand()
				fd 1
			}
		}
	}

	function get-away () {
		rt rand()
		fd 20
		while (zone.color != black) {
			rt rand()
			fd 20
		}
	}

	function wiggle () {
		fd 1
		rt rand() % 50
		lt rand() % 50
	}

	teleport(rand() % world.max-x, rand() % world.max-y, rand())

	while (true) {
		go()
	}
}

function init-zone() {
	test = rand() % 3
	if (test == 0) {
		color = yellow
	}
	else {
		color = black
	}
}

foreach-zone (init-zone)

20 new termite()
