/*
Ce programme inspiré d'un exemple de NetLogo, présente les termites.
Le but de ce programme est la construction d'une termitière par des termites.

Elles doivent pour cela regrouper des brindilles.
Au départ, les zones sont aléatoirement coloriées en noir et jaune, le jaune correspondant à des brindilles.
On crée ensuite des agents qui se baladent aléatoirement.
Lorqu'ils croisent une brindille, ils la prennent et la portent jusqu'à trouver un nouveau tas.

Lors du déroulement de la simulation, des tas doivent être créés.
*/

%world_width 100
%world_height 100
%zone_width 5
%zone_height 5
%x_border WRAP
%y_border WRAP

agent termite () {
	function go () {
		search_for_chip ()
		find_new_pile ()
		put_down_chip ()
	}

	function search_for_chip () {
		continue = true
		while continue {
			if (zone.color == yellow) {
				zone.color = black
				color = red
				fd 20
				continue = false
			}
			else {
				wiggle()
			}
		}
	}

	function find_new_pile () {
		while zone.color != yellow {
			wiggle()
		}
	}

	function put_down_chip () {
		continue = true
		while continue {
			if zone.color == black {
				zone.color = yellow
				color = white
				get_away()
				continue = false
			}
			else {
				wiggle()
			}
		}
	}

	function get_away () {
		while zone.color != black {
			wiggle()
		}
	}

	function wiggle () {
		fd 1
		rt rand() % 50
		lt rand() % 50
	}

	teleport(rand() % world.max_x, rand() % world.max_y, rand())

	while (true) {
		go()
	}
}

function init_zone() {
	rand = world.rand
	test = rand() % 5
	if (test == 0) {
		color = world.yellow
	}
	else {
		color = world.black
	}
}

ask_zones (init_zone)

20 new termite()
